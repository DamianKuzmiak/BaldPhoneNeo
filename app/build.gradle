/*
 * Copyright 2019 Uriah Shaul Mandel
 * Copyright 2025 Damian Kuzmiak
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def keystorePropertiesFile = rootProject.file('app/keystore.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

apply plugin: 'com.android.application'

android {
    namespace "com.bald.uriah.baldphone"
    compileSdk 34
    defaultConfig {
        applicationId "app.baldphone.neo"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 28
        versionCode 106
        versionName "15.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fullApp: "true", recentCalls: "true"]
    }

    buildFeatures {
        buildConfig true
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file('release-key.jks')
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "updating_mechanism"
    productFlavors {
        baldUpdates {
            dimension "updating_mechanism"
        }
        fDroid {
            dimension "updating_mechanism"
        }
        gPlay {
            dimension "updating_mechanism"
            applicationIdSuffix ".gp"
            manifestPlaceholders = [fullApp: "true", recentCalls: "false"]
        }
        gPlayRecentCalls {
            dimension "updating_mechanism"
            applicationIdSuffix ".gprc"
            manifestPlaceholders = [fullApp: "false", recentCalls: "true"]
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.room:room-runtime:2.7.2'
    annotationProcessor 'androidx.room:room-compiler:2.7.2'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core:1.9.0'

    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'  // [Long Term] Migrate away from it.
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'net.danlew:android.joda:2.10.6'
    implementation 'com.github.nisrulz:lantern:1.1.2'  // Version 2.0.0 was unavailable from JitPack

    implementation 'com.android.volley:volley:1.2.1'

    // Only for tests
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'com.github.blainepwnz:AndroidContacts:1.14.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'junit:junit:4.13'
}
